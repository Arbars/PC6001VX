#
# qmake configuration for linux-g++
#

#Pretty good source of SGX information although it is about the pandaboard
#http://tigraphics.blogspot.com/2011/07/running-qtgles-on-omap4.html

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

MAKEFILE_GENERATOR	= UNIX
QMAKE_PLATFORM		+= pandora embedded

GCC_PREFIX=$$(PNDSDK)/bin/arm-none-linux-gnueabi

# modifications to g++.conf
QMAKE_CC                = $${GCC_PREFIX}-gcc
QMAKE_CXX               = $${GCC_PREFIX}-g++
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${GCC_PREFIX}-ar cqs
QMAKE_OBJCOPY           = $${GCC_PREFIX}-objcopy
QMAKE_STRIP             = $${GCC_PREFIX}-strip
                                                                   
DEFINES += PANDORA 

QMAKE_CFLAGS_RELEASE    = -O2 -pipe -mcpu=cortex-a8 -mfpu=neon -mfloat-abi=softfp -fPIC -ftree-vectorize -ffast-math
QMAKE_CXXFLAGS_RELEASE  = $$QMAKE_CFLAGS_RELEASE

QMAKE_CFLAGS_DEBUG    = -g -O0 -mcpu=cortex-a8 -mfpu=neon -mfloat-abi=softfp
QMAKE_CXXFLAGS_DEBUG  = $$QMAKE_CFLAGS_DEBUG

QMAKE_INCDIR          = $$(PNDSDK)/usr/include $$(PNDSDK)/include 

QMAKE_LFLAGS_RPATH	= -Wl,-rpath,                
QMAKE_LFLAGS += $${QMAKE_LFLAGS_RPATH}$$(PNDSDK)/usr/lib
QMAKE_LFLAGS += $${QMAKE_LFLAGS_RPATH}$$(PNDSDK)/lib
QMAKE_LIBDIR += $$(PNDSDK)/usr/lib $$(PNDSDK)/lib

# Work round PowerVR SGX 1.3 driver bug with glScissor & FBOs:
DEFINES += QT_GL_NO_SCISSOR_TEST

# Work round SGX 1.4 driver bug (text corrupted), modify glyph cache width:
DEFINES += QT_DEFAULT_TEXTURE_GLYPH_CACHE_WIDTH=1024

#testlibのコンパイルが通す方法がわからないので暫定対策
DEFINES += SYS_perf_event_open=0

load(qt_config)
