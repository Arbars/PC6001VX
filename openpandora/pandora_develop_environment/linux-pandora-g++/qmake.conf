#
# qmake configuration for linux-g++
#

#Pretty good source of SGX information although it is about the pandaboard
#http://tigraphics.blogspot.com/2011/07/running-qtgles-on-omap4.html

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

MAKEFILE_GENERATOR	= UNIX
QMAKE_PLATFORM		+= pandora embedded

PANDORA_SDK = $$PNDSDK

GCC_PREFIX = $$PANDORA_SDK/bin/arm-none-linux-gnueabi

# modifications to g++.conf
QMAKE_CC                = $${GCC_PREFIX}-gcc
QMAKE_CXX               = $${GCC_PREFIX}-g++
QMAKE_LINK              = $${QMAKE_CXX}
QMAKE_LINK_SHLIB        = $${QMAKE_CXX}

# modifications to linux.conf
QMAKE_AR                = $${GCC_PREFIX}-ar cqs
QMAKE_OBJCOPY           = $${GCC_PREFIX}-objcopy
QMAKE_STRIP             = $${GCC_PREFIX}-strip
                                                                   
DEFINES += PANDORA 

QMAKE_CFLAGS_RELEASE    = -O2 -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp  -ftree-vectorize -ffast-math -Wno-psabi 
QMAKE_CXXFLAGS_RELEASE  = $$QMAKE_CFLAGS_RELEASE

QMAKE_CFLAGS_DEBUG    = -g -O0 -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp  -Wno-psabi 
QMAKE_CXXFLAGS_DEBUG  = $$QMAKE_CFLAGS_DEBUG

QMAKE_INCDIR          = $${PANDORA_SDK}/usr/local/qt5/include $${PANDORA_SDK}/usr/include $${PANDORA_SDK}/include 

QMAKE_LFLAGS_RPATH	= -Wl,-rpath,                
QMAKE_LFLAGS += $${QMAKE_LFLAGS_RPATH}$${PANDORA_SDK}/usr/lib
QMAKE_LFLAGS += $${QMAKE_LFLAGS_RPATH}$${PANDORA_SDK}/lib
QMAKE_LIBDIR += $${PANDORA_SDK}/usr/local/qt5/lib $${PANDORA_SDK}/usr/lib $${PANDORA_SDK}/lib 

QMAKE_LIBS_EGL          = -lEGL -lIMGegl -lsrv_um
QMAKE_LIBS_OPENGL_ES2   = -lGLESv2 $${QMAKE_LIBS_EGL}
QMAKE_LIBS_OPENVG       = -lOpenVG $${QMAKE_LIBS_EGL}

# Work round PowerVR SGX 1.3 driver bug with glScissor & FBOs:
#DEFINES += QT_GL_NO_SCISSOR_TEST

# Work round SGX 1.4 driver bug (text corrupted), modify glyph cache width:
#DEFINES += QT_DEFAULT_TEXTURE_GLYPH_CACHE_WIDTH=1024

#testlibのコンパイルが通す方法がわからないので暫定対策
DEFINES += SYS_perf_event_open=0

#EGLFSとの共存に必要
DEFINES += QT5_EGL SUPPORT_X11

load(qt_config)
